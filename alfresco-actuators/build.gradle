plugins {
    id 'java'
    id 'eu.xenit.alfresco' version "1.0.1"
    id 'eu.xenit.amp' version "1.0.1"
    id 'org.hidetake.swagger.generator' version '2.11.0'
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

description = "Xenit webscripts to retrieve health of the application"
group = 'eu.xenit.alfresco-actuators'

// compile against alfresco 5.2
ext {
    alfrescoVersion='5.2.7'
    servletApiVersion = '2.5'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    alfrescoProvided("org.alfresco:alfresco-repository:$alfrescoVersion")
    alfrescoProvided("org.alfresco:alfresco-enterprise-repository:$alfrescoVersion")
    alfrescoProvided("javax.servlet:servlet-api:$servletApiVersion")

    // Add dependency for Swagger Codegen CLI
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.3'

    compileOnly 'io.swagger:swagger-annotations:1.5.6'
    compileOnly 'javax.validation:validation-api:1.1.0.Final'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.0'
    compile("javax.annotation:javax.annotation-api:1.3.2")

}

swaggerSources {

    model {
        inputFile = file("src/main/resources/swagger.yaml")
        code {
            language = 'spring'
            additionalProperties = [
                    "modelPackage"     : "eu.xenit.actuators.model.gen",
                    "serializableModel": "true"
            ]
            // Only generate model:
            components = ['models']
        }
    }

}

processResources {
    filesMatching("**/module.properties") {
        expand project.properties
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java","$generateSwaggerCodeModel.outputDir/src/main/java"]
        }
        resources {
            srcDirs = ['src/main/resources']
        }
        amp {
            module("src/main/resources/alfresco/module/alfresco-actuators/module.properties")
        }
    }
}


compileJava.dependsOn generateSwaggerCodeModel

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.amp
        }
    }
}



