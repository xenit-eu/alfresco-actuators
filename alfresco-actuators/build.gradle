import java.nio.file.Paths

plugins {
    id 'java'
    id 'maven-publish'
    id 'eu.xenit.amp' apply false
    id 'eu.xenit.alfresco' apply false
    id "com.github.johnrengelman.shadow" version "7.1.2" apply false
}

description = "Amp to add health check endpoint"

subprojects {
    apply plugin: 'eu.xenit.amp'
    apply plugin: 'eu.xenit.alfresco'
    apply plugin: 'com.github.johnrengelman.shadow'

    targetCompatibility = 17
    sourceCompatibility = 17

    description = project(':alfresco-actuators').getProperties().get("description")

    java.nio.file.Path versionsGradlePath = Paths.get(project(":alfresco-actuators").projectDir.path, project.name, "versions.gradle")
    apply from: versionsGradlePath

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        alfrescoProvided platform("org.alfresco:acs-packaging:${alfrescoVersion}")
        alfrescoProvided("org.alfresco:alfresco-repository")
        alfrescoProvided("org.alfresco:alfresco-enterprise-repository")
        alfrescoProvided("jakarta.servlet:jakarta.servlet-api:$servletApiVersion")
        alfrescoProvided 'jakarta.annotation:jakarta.annotation-api'
        alfrescoProvided 'com.fasterxml.jackson.core:jackson-annotations'

        implementation platform("org.alfresco:acs-packaging:${alfrescoVersion}")
        implementation "jakarta.validation:jakarta.validation-api:${jakartaValidationApi}"
        implementation "io.swagger.core.v3:swagger-annotations-jakarta:${swaggerAnnotationsJakarta}"
    }

    apply from: "${rootProject.projectDir}/swaggerCodeGen.gradle"

    tasks.named("sourcesJar"){
        dependsOn(tasks.named("generateSwaggerCodeModel"))
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact tasks.amp
            }
        }
    }

    sourceSets {
        main {
            java.srcDirs "${project(':alfresco-actuators').projectDir}/src/main/java"
            amp {
                config.srcDir("${project(':alfresco-actuators').projectDir}/src/main/amp/config")
                web.srcDir("${project(':alfresco-actuators').projectDir}/src/main/amp/web")
                module {
                    it.put("module.id", project(':alfresco-actuators').name)
                    it.put("module.title", project(':alfresco-actuators').name)
                    it.put("module.description", project(':alfresco-actuators').getProperties().get("description"))
                    it.put("module.version", project(':alfresco-actuators').version)
                    it.put("module.repo.version.min", "23.0")
                }
            }
        }
    }
}